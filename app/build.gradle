plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

def getApiKey() {
    def propFile = project.rootProject.file('local.properties')
    def properties = new Properties()
    properties.load(propFile.newDataInputStream())
    return properties['TWITCH_CLIENT_ID']
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.twitchapp"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField ("String", "CLIENT_ID", getApiKey())
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        abortOnError false
    }
    buildFeatures {
        viewBinding true
    }
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

configurations {
    ktlint
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Truth
    androidTestImplementation "com.google.truth:truth:1.1.3"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"

    // LiveData テスト用
    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    testImplementation "android.arch.core:core-testing:1.1.1"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"

    // Moshi
    implementation 'com.squareup.moshi:moshi:1.11.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.11.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // RecyclerView CardView
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Room
    implementation "androidx.room:room-runtime:2.4.0"
    kapt "androidx.room:room-compiler:2.4.0"

    androidTestImplementation "android.arch.persistence.room:testing"

    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.4.0"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation "android.arch.navigation:navigation-fragment:1.0.0"
    implementation "android.arch.navigation:navigation-ui:1.0.0"

    implementation "androidx.fragment:fragment-ktx:1.3.6"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // SwipeLayout
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'

    // SwipeRefresh
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // fragment-ktx
    implementation "androidx.fragment:fragment-ktx:1.3.6"

    // Activity Result API
    implementation 'androidx.activity:activity-ktx:1.3.1'

    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // Dagger-Hilt
    implementation "com.google.dagger:hilt-android:2.33-beta"
    kapt "com.google.dagger:hilt-android-compiler:2.33-beta"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"
    kapt "androidx.hilt:hilt-compiler:1.0.0-beta01"

    // Chrome Custom Tabs
    implementation "androidx.browser:browser:1.4.0"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.1.0"

    // ktlint
    ktlint("com.pinterest:ktlint:0.42.1")
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "--android", "--color", "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/reports/ktlint-result.xml", "src/**/*.kt"
    ignoreExitValue true
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}
